pkgname=uprt-git
pkgver=v2021.10.001.beta.r17.g78a5a52
pkgrel=1
pkgdesc="A tool to migrate static leases between DD-WRT, OpenWrt, OPNsense, Mikrotik, and pfSense routers"
arch=('x86_64')
url="https://github.com/GeekVisit/uproot"
makedepends=('dart' 'git')
provides=('uprt')
conflicts=('uprt')
options=(!strip)
source=("$pkgname::git+https://github.com/GeekVisit/uproot.git")
sha256sums=('SKIP')

pkgver() {
    cd "$pkgname"
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$pkgname"
    dart pub get
    dart compile exe -Dversion=$pkgver bin/uprt.dart -o bin/uprt
}

check() {
    cd "$pkgname"
    dart test
    ./bin/uprt test/test-data/lease-list-infile.csv -g cdjmnop -d test/test-output
}

package() {
    cd "$pkgname"
    install -Dm 755 bin/uprt "${pkgdir}/usr/bin/uprt"
}
